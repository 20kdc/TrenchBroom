Expression     = GroupedTerm | Term
GroupedTerm    = "(" Term ")"
Term           = SimpleTerm | CompoundTerm
SimpleTerm     = Variable | Literal | Subscript | Plus | Minus | GroupedTerm
CompoundTerm   = Addition | Subtraction | Multiplication | Division | Modulus

Variable       = ( Alpha | "_" ) { ( Alpha | Numeric | "_") }
Literal        = String | Number | Boolean | Array | Map

Subscript      = SimpleTerm "[" ArraySpec "]"
Addition       = SimpleTerm "+" Expression
Subtraction    = SimpleTerm "-" Expression
Multiplication = SimpleTerm "*" Expression
Division       = SimpleTerm "/" Expression
Modulus        = SimpleTerm "%" Expression

Plus           = "+" SimpleTerm
Minus          = "-" SimpleTerm

Boolean        = "true" | "false"
String         = """ { Char } """
Integer        = Numeric { Numeric }
Number         = Integer "." Numeric { Numeric }

Array          = "[" [ ArraySpec ] "]"
ArraySpec      = ExpOrRange { "," ExpOrRange }
ExpOrRange     = ( Expression | Range )
Range 		   = Expression ".." Expression

Map            = "{" [ KeyValuePair { "," KeyValuePair } ]  "}"
KeyValuePair   = String ":" Expression

Alpha          = "a-zA-Z"
Numeric        = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
Char           = Any ASCII character
