#version 120

/*
 Copyright (C) 2010-2012 Kristian Duske
 
 This file is part of TrenchBroom.
 
 TrenchBroom is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 (at your option) any later version.
 
 TrenchBroom is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.
 
 You should have received a copy of the GNU General Public License
 along with TrenchBroom.  If not, see <http://www.gnu.org/licenses/>.
 */

uniform float Brightness;
uniform bool ApplyTexture;
uniform sampler2D FaceTexture;
uniform bool ApplyTinting;
uniform vec4 TintColor;
uniform bool GrayScale;
uniform bool RenderGrid;
uniform float GridSize;
uniform vec4 GridColor;

varying vec4 modelCoordinates;
varying vec3 modelNormal;
varying vec4 faceColor;

void gridXY() {
    if (abs(mod(modelCoordinates.x + 0.5, 64)) <= 0.6 ||
        abs(mod(modelCoordinates.y + 0.5, 64)) <= 0.6)
        gl_FragColor = vec4(mix(gl_FragColor.rgb, GridColor.rgb, GridColor.a * 1.5), gl_FragColor.a);
    else if (abs(mod(modelCoordinates.x + 0.5, GridSize)) <= 0.6 ||
        abs(mod(modelCoordinates.y + 0.5, GridSize)) <= 0.6)
        gl_FragColor = vec4(mix(gl_FragColor.rgb, GridColor.rgb, GridColor.a), gl_FragColor.a);
}

void gridXZ() {
    if (abs(mod(modelCoordinates.x + 0.5, 64)) <= 0.6 ||
        abs(mod(modelCoordinates.z + 0.5, 64)) <= 0.6)
        gl_FragColor = vec4(mix(gl_FragColor.rgb, GridColor.rgb, GridColor.a * 1.5), gl_FragColor.a);
    else if (abs(mod(modelCoordinates.x + 0.5, GridSize)) <= 0.6 ||
        abs(mod(modelCoordinates.z + 0.5, GridSize)) <= 0.6)
        gl_FragColor = vec4(mix(gl_FragColor.rgb, GridColor.rgb, GridColor.a), gl_FragColor.a);
}

void gridYZ() {
    if (abs(mod(modelCoordinates.y + 0.5, 64)) <= 0.6 ||
        abs(mod(modelCoordinates.z + 0.5, 64)) <= 0.6)
        gl_FragColor = vec4(mix(gl_FragColor.rgb, GridColor.rgb, GridColor.a * 1.5), gl_FragColor.a);
    else if (abs(mod(modelCoordinates.y + 0.5, GridSize)) <= 0.6 ||
        abs(mod(modelCoordinates.z + 0.5, GridSize)) <= 0.6)
        gl_FragColor = vec4(mix(gl_FragColor.rgb, GridColor.rgb, GridColor.a), gl_FragColor.a);
}

void main() {
    if (ApplyTexture) {
        gl_FragColor = texture2D(FaceTexture, gl_TexCoord[0].st);
    } else {
        gl_FragColor = faceColor;
    }
    
    gl_FragColor = vec4(vec3(Brightness / 2.0 * gl_FragColor), gl_FragColor.a);
    gl_FragColor = clamp(2.0 * gl_FragColor, 0.0, 1.0);

    if (GrayScale) {
        float gray = dot(gl_FragColor.rgb, vec3(0.299, 0.587, 0.114));
        gl_FragColor = vec4(gray, gray, gray, gl_FragColor.a);
    }
    
    if (ApplyTinting) {
        gl_FragColor = vec4(gl_FragColor.rgb * TintColor.rgb * TintColor.a, gl_FragColor.a);
        gl_FragColor = clamp(2.0 * gl_FragColor, 0.0, 1.0);
    }
    
    if (RenderGrid) {
        float normX = abs(modelNormal.x);
        float normY = abs(modelNormal.y);
        float normZ = abs(modelNormal.z);
        if (normX > normY) {
            if (normX > normZ)
                gridYZ();
            else
                gridXY();
        } else if (normY > normZ) {
            gridXZ();
        } else {
            gridXY();
        }
    }
}
