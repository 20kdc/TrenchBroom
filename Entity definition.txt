entdef 		= defopen header newline [body] defclose
header		= name [color bounds {flag}]
color		= "(" frac frac frac ")"
bounds		= "?" | point point
point		= "(" dec dec dec ")"
flag		= word

body		= [props] [desc]
props		= "{" prop {prop} "}"
prop		= choiceprop | modelprop | baseprop | defaultprop
choiceprop	= "choice" qstring "(" {choiceval} ")" ";"
choicevalue	= "(" dec "," qstring ")"
modelprop	= "model" "(" qstring ")" ";"
baseprop	= "base" "(" qstring ")" ";"
defaultprop	= "default" "(" qstring "," qstring ")" ";"
desc		= string

defopen		= "/*QUAKED"
defclose	= "*/"
word		= wordchar {wordchar}
wordchar	= digit | "a" ... "z" | "A" ... "Z" | "_"
qstring		= """ string """


string		= stringchar {stringchar}
stringchar	= char | digit | newline
newline 	= "\n"
dec 		= ["-"] digit {digit}
frac 		= dec | dec "." digit {digit}
